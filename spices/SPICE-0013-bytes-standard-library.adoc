= Bytes standard library class

* Proposal: link:./SPICE-0013-bytes-standard-library.adoc[SPICE-0013]
* Author: https://github.com/bioball[Dan Chao]
* Status: TBD
* Implemented in: TBD
* Category: Language, Standard Library

== Introduction

This SPICE introduces a new value type called `Bytes`, which represents binary data.

== Motivation

Pkl currently does not have a binary data type.

This introduces the following limitations:

* `pkl eval` can only produce UTF-8 encoded strings.
* Asides from values coming from `read()`, it is not possible to compute checksums of binary content.
* It is harder to interop with configuration formats that allow binary data.

== Proposed Solution

A new class will be added to `pkl:base`, called `Bytes`.

== Detailed design

=== Standard library changes

The following changes will be made to the standard library

==== `Bytes` class

A new class will be added to `pkl:base`, called `Bytes`.
This will be a new value type, similar to `List`, `Map`, and `IntSeq`.

[source,pkl]
----
@Since { version = "0.29.0" }
external class Bytes extends Any {
  /// The length of these bytes.
  external length: Int

  /// The number of bytes as [DataSize].
  external size: DataSize

  /// The value represented by these bytes, in base64 encoding.
  external base64: String(isBase64)

  /// The value represented by these bytes, in hexadecimal encoding.
  external hex: String

  /// The [MD5](https://en.wikipedia.org/wiki/MD5) hash of these bytes as hexadecimal string.
  ///
  /// MD5 is cryptographically broken and should not be used for secure applications.
  external md5: String

  /// The [SHA-1](https://en.wikipedia.org/wiki/SHA-1) hash of these bytes as hexadecimal string.
  ///
  /// SHA-1 is cryptographically broken and should not be used for secure applications.
  external sha1: String

  /// The [SHA-256](https://en.wikipedia.org/wiki/SHA-2) cryptographic hash of these bytes as
  /// hexadecimal string.
  external sha256: String

  /// The first 64 bits of the [SHA-256](https://en.wikipedia.org/wiki/SHA-2) cryptographic hash of
  /// these bytes.
  external sha256Int: Int

  /// Returns the element at [index].
  ///
  /// Returns [null] if [index] is outside the bounds of these Bytes.
  ///
  /// Facts:
  /// ```
  /// Bytes(3, 9, 6).getOrNull(0) == 3
  /// Bytes(3, 9, 6).getOrNull(1) == 9
  /// Bytes(3, 9, 6).getOrNull(2) == 6
  /// Bytes(3, 9, 6).getOrNull(-1) == null
  /// Bytes(3, 9, 6).getOrNull(3) == null
  /// Bytes(3, 9, 6).getOrNull(99) == null
  /// ```
  external function getOrNull(index: Int): UInt8?

  /// Converts these bytes into a string, encoded using [charset].
  ///
  /// Throws if these bytes are invalid according to the given [charset].
  external function decodeToString(charset: Charset): String

  /// Converts these bytes into a [List].
  external function toList(): List<UInt8>
}
----

Bytes are built using the `Bytes` constructor method.
Like `List`, `Set` and `Map`, the constructor receives a variable number of arguments.

[source,pkl]
----
/// Creates [Bytes] from the given numbers.
///
/// Examples:
/// ```
/// bytes = Bytes(0x1, 0x2, 0x3, 0x4)
/// ```
@Since { version = "0.29.0" }
external const function Bytes(values: VarArgs<UInt8>): Bytes
----

=== `toString()`

The string representation of `Bytes` will be `Bytes(<numbers>)`.
For example, bytes `0x1`, `0x2`, `0x3`, `0x4` is rendered as `Bytes(1, 2, 3, 4)`.

=== In-language value renderer

When bytes are rendered in-language (for example, in error messages), they are possibly truncated, with the size of the bytes shown.

[source]
----
–– Pkl Error ––
Type constraint `myConstraint` failed.

Value: Bytes(1, 2, 3, 4, 5, 6, 7, 8, ... <5.2.mb more bytes>)
----

At most 8 bytes are shown, and the rest is truncated.

This rendering is used for error messages, and also for REPL output.

=== Comparison

Bytes can be compared using the `==` and `!=` operators.

Two `Bytes` are considered equal if they have the same length, and if numbers are sequentially equivalent.

[source,pkl]
----
facts {
  Bytes() == Bytes()
  Bytes(1, 2, 3) == Bytes(1, 2, 3)
  Bytes(1, 2, 3) != Bytes(1, 2, 4)
  Bytes() != Set()
}
----

=== Subscript

Elements of `Bytes` can be accessed using subscript, where the subscript receives an integer index.

The result of subscript is a `UInt8`, or throws an exception if the index if out of bounds.

[source,pkl]
----
bytes = Bytes(1, 2, 3, 4)
res1 = bytes[0]
res2 = bytes[1]
----

Possibly out-of-bounds access can be guarded using `getOrNull`:

[source,pkl]
----
bytes = Bytes(1, 2, 3, 4)
res1 = bytes.getOrNull(5) // <1>
res2 = bytes.getOrNull(2) // <2>
----
<1> Result: `null`
<2> Result: `3`

=== Concatenation

Two `Bytes` values can be concatenated using `+`, producing the combined result of both values.

[source,pkl]
----
Bytes(1, 2, 3, 4) + Bytes(5, 6, 7, 8) // <1>
----
<1> Result: `Bytes(1, 2, 3, 4, 5, 6, 7, 8)`

=== Generators

`Bytes` can be iterated over by a for-generator, and spread syntax.

For-generators receive an integer key, and a `UInt8` value.

The following code:

[source,pkl]
----
bytes: Bytes = Bytes(1, 2, 3, 4)

elems {
  for (idx, elem in bytes) {
    "My index is \(idx), and my byte is \(elem)"
  }
}

elems2 {
  ...bytes
}
----

Produces the following PCF:

[source,pkl]
----
bytes = Bytes(1, 2, 3, 4)
elems {
  "My index is 0, and my byte is 1"
  "My index is 1, and my byte is 2"
  "My index is 2, and my byte is 3"
  "My index is 3, and my byte is 4"
}
elems2 {
  1
  2
  3
  4
}
----

`Bytes` can only be spread into `Dynamic` or `Listing`.
Spreading into a `Mapping` or `Typed` is an error.

[source,pkl]
----
bytes = Bytes(1, 2, 3, 4)

mapping: Mapping = new {
  ...bytes // <1>
}
----
<1> Error: cannot spread value of type `Bytes` into object of type `Mapping`.

=== `String` class

The `String` class will receive the following new members:

[source,pkl]
----
class String {
  /* etc */

  /// Tells if this is a valid base64-encoded string.
  ///
  /// Facts:
  /// ```
  /// "AQIDBA==".isBase64
  /// !"hello there".isBase64
  /// ```
  @Since { version = "0.29.0" }
  external isBase64: Boolean

  /// Converts this base64-format string into [Bytes].
  ///
  /// Throws if this is not a base64-encoded string.
  ///
  /// Facts:
  /// ```
  /// "AQIDBA==".base64DecodedBytes = Bytes(1, 2, 3, 4)
  /// ```
  @Since { version = "0.29.0" }
  external base64DecodedBytes: Bytes

  /// Returns the bytes of this string, encoded using [charset]. 
  ///
  /// Facts:
  /// ```
  /// "Parrot".encodeToBytes("UTF-8") == Bytes(80, 97, 114, 114, 111, 116)
  /// ```
  @Since { version = "0.29.0" }
  function encodeToBytes(charset: Charset): Bytes
}
----

=== `List` class

The `List` class will receive the following new members:

[source,pkl]
----
external class List<out Element> extends Collection<Element> {
  /* etc */

  /// Converts this list to [Bytes].
  ///
  /// Throws a type error if any elements are not [UInt8].
  external function toBytes(): Bytes
}
----

=== `Charset` typealias

A new typealias called `Charset` will be added to the base module.

[source,pkl]
----
@Since { version = "0.29.0" }
typealias Charset = "UTF-8"|"UTF-16"|"ISO-8859-1"
----

=== `Resource` class

The following property will be added to the `Resource` class.

[source,pkl]
----
class Resource {
  /* etc */

  /// The bytes of this resource.
  @Since { version = "0.29.0" }
  external bytes: Bytes
}
----

Additionally, the existing properties `base64`, `md5`, `sha1`, `sha256`, `sha256Int` are deprecated, and to be replaced by calling the same properties on `bytes`, or setting `bytes` (in the case of `base64`).

=== `FileOutput` class

A new property is added to the `FileOutput` class, called `bytes`.
It defaults to the UTF-8 bytes of `text`.

[source,pkl]
----
class FileOutput {
  /* etc */

  /// The textual rendered output.
  text: String = renderer.renderDocument(value)

  /// The underlying byte array of [text], in UTF-8 encoding.
  @Since { version = "0.29.0" }
  bytes: Bytes = text.toBytes()
}
----

=== Renderer changes

Some renderers will be able to render `Bytes` out of the box. These are:

* PcfRenderer
* PlistRenderer

The following Pkl:

[source,pkl]
----
res = Bytes(1, 2, 3, 4)
----

Produces the following PCF:

[source,pkl]
----
res = Bytes(1, 2, 3, 4)
----

Produces the following Plist:

[source,xml]
----
<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE plist PUBLIC "-//Apple//DTD PLIST 1.0//EN" "http://www.apple.com/DTDs/PropertyList-1.0.dtd">
<plist version="1.0">
<dict>
  <key>res</key>
  <data>AQIDBA==</data>
</dict>
</plist>
----

All other renderers will require a converter to be defined for `Bytes`, and throw an error otherwise.

=== Pkl Binary Encoding changes

When encoded to https://pkl-lang.org/main/current/bindings-specification/binary-encoding.html[Pkl Binary Encoding], `Bytes` will be encoded as an array of two elements.

The first element is `0xF`.

The second element is the binary contents of `Bytes`.

=== Codegen changes

Code generators should convert `bytes` into the respective type in the target language.

These will be the mappings for the libraries maintained by the core team:

|===
|Language |Type

|Java
|`byte[]`

|Kotlin
|`ByteArray`

|Go
|`[]byte`

|Swift
|`[UInt8]`
|===

=== Changes to `pkl eval`

By default, `pkl eval` will change to evaluate `output.bytes` of a module.
Additionally, `pkl eval -m` will change to evaluate the `output.files[*].bytes` of each file output.

These raw bytes will be written either to standard out, or to the specified output file.

This change means that it is possible for Pkl to write any content, not just UTF-8 encoded strings.

=== Changes to `org.pkl.core.Evaluator`

The `evaluateExpression` method will map Pkl type `Bytes` to `byte[]` in Java.

A new method will be added, called `evaluateOutputBytes`, which evaluates the `output.bytes` of a module.

[source,java]
----
interface Evaluator {
  /**
   * Evaluates a module's {@code output.bytes} property.
   */
  byte[] evaluateOutputBytes(ModuleSource moduleSource);
}
----

=== Examples

==== Creating `Bytes`

[source,pkl]
----
// From a list
bytes1 = Bytes(1, 2, 3, 4)

// From a base64-encoded string
bytes2 = "AQIDBA==".base64DecodedBytes
----

==== Concatenating two `Bytes`

[source,pkl]
----
bytes3: Bytes = bytes1 + bytes2
----

==== Transforming bytes

`Bytes` is optimized for access, and not for transformations.

Transformations can be done by converting to `List`.

[source,pkl]
----
bytes1: Bytes

bytes2: Bytes = bytes1.toList().take(4).toBytes()
----

==== String conversions

[source,pkl]
----
bytes: Bytes = Bytes(80, 97, 114, 114, 111, 116)

myStr: String = bytes.decodeToString("UTF-8") // "Parrot"
----

==== Set raw file output

[source,pkl]
----
output {
  files {
    ["file.bin"] {
      bytes = read("path/to/my/file.bin").bytes
    }
  }
}
----

==== Compute a certificate's fingerprint

[source,pkl]
----
local certText = read("path/to/my/cert.pem").text
local certificateBytes = certText
  .replaceFirst("-----BEGIN CERTIFICATE-----", "")
  .replaceLast("-----END CERTIFICATE-----", "")
  .replaceAll("\n", "")
  .base64DecodedBytes
fingerprint = certificateBytes.sha256
----

== Compatibility

Because of how Pkl resolves names, it is always a breaking change to add a new name to the base module.

Pkl's name resolution rules are:

1. Look for the name in the lexical scope.
2. Look for the name in the base module.
3. Look for the name off of implicit `this`.

This means that any conflicting names that are resolved from implicit `this`, they will need to be changed to the explicit `this`.

The new names added are:

* `Bytes`
* `Charset`

== Future directions

=== Binary Value Renderers

One of the upcoming Pkl roadmap items is to have an in-language renderer and parser that works with Pkl Binary Format.

The existence of a `Bytes` type sets up for such a renderer. Imagined API:

[source,pkl]
----
class BinaryValueRenderer {
  function renderDocument(value: Any): Bytes

  function renderValue(value: Any): Bytes
}
----

This also allows for other binary renderers (for example, msgpack renderer, binary plist renderer, protobuf renderer).

Possibly, a binary value renderer can be used in conjunction with a module's output.

The existing module output can be re-defined as:

[source,pkl]
----
hidden output: ModuleOutput = new {
  text =
    if (renderer is BinaryValueRenderer) super.text
    else renderer.renderDocument(value)
  bytes =
    if (renderer is BinaryValueRenderer) renderer.renderBytes(value)
    else super.bytes
}
----

=== Additional methods on `Bytes`

`Bytes` intentionally does not provide methods for transformations.
While this can be useful, these use-cases are not commonly aligned with Pkl's goal of being used for configuration.

Thus, methods like `map`, `flatMap`, `filter`, etc, are omitted.
Instead, users are expected to cast to `List` and back to `Bytes` as a workaround.

If a sufficient need for this arises in the future, these methods can be implemented.
Additionally, `Bytes` can perhaps be another subclass of `Collection` (the superclass of `Set` and `List`).

== Alternatives considered

=== `Bytes` as a regular class

This proposal introduces `Bytes` as a value type (similar to `Duration` and `DataSize`).

An alternative approach is to make this class a regular class (which makes it a `Typed`).

However, using a regular class makes it hard to optimize in the underlying implementation.

When implemented as a value type, it can be backed by its own special representation (and ultimately as Java `byte[]`).
This is much more challenging when represented as a regular `Typed` object.

Additionally, `Typed` values (like other `Object` types) are designed to be amended, and have late-binding semantics.
These semantics are not useful for byte arrays.

Furthermore, libraries would have additional overhead.
For example, the Java API would expose any `Bytes` values as a `PObject`.
Additionally, the Pkl Binary Encoding would represent this using its existing `Typed` encoding.

=== `Int8` instead of `UInt8`

Bytes are commonly represented in hexadecimal notation, from `0x00` to `0xff`.

Some languages, like Java, interop from bytes into signed int types.
For example, casting a byte to an int can possibly produce a negative integer:

[source,java]
----
byte b = (byte) 0xc0;
int i = (int) b;
System.out.println(i); // <1>
----
<1> Prints `-64`.

Here, value `0xc0` is treated as a https://en.wikipedia.org/wiki/Two%27s_complement[two's complement] signed 8-bit integer.

In Pkl, `0xc0` is always equivalent to `192`.
If `Bytes` were treated as a sequence of `Int8` numbers, the expression `Bytes(0xc0)` would error, because 196 is greater than `Int8` bounds.
However, this would be at odds with how bytes are commonly described.

=== Bytes as a wrapper

Conceptually, `Bytes` and `List<UInt8>`/`Listing<UInt8>` are all sequences of `UInt8` numbers.
They both allows subscript access, and iteration via generators.

To avoid this duplication, `Bytes` can simply be a wrapper class, where it is only used to semantically identify when values should be treated as binary data.
The underlying data would be processed by unwrapping.

[source,pkl]
----
class Bytes {
  data: List<UInt8>
}
----

However, there are some drawbacks to this approach.

==== Heap space

A quick profile shows that allocating 1000 numbers in a `List` takes 5.4Kb of heap space when run in the JVM.
The implementation of `List` incurs some overhead for each member, and this is quite expensive when each member is only byte-sized.

In contrast, `Bytes` is a specialized class that can be compactly stored, with heap usage largely mirroring the amount of allocated in-language bytes.

==== Specializing `List<UInt8>` is challenging

In theory, `List<UInt8>` can also be specialized into a contiguous byte array.
However, Pkl's model makes this type of specialization challenging.
In particular, Pkl doesn't actually use differently sized integers; all integers are values of class `Integer`.

It's possible to specialize `List<UInt8>` if Pkl's evaluation is guided by type annotations.
However, it's easy to de-specialize:

[source,pkl]
----
// okay; can specialize here
foo: List<UInt8> = List(1, 2, 3, 4) 

// can no longer specialize; we don't know if the result of `map` can fit within `UInt8`
foo2 = foo.map((it) -> it + 1) 
----

Additionally, `List` values do not retain their type parameter.
Pkl does not know if an incoming `List` is all of type `UInt8` without checking every element.

This approach was explored in https://github.com/bioball/pkl/tree/vm-byte-array-list.

== Acknowledgements

Thanks to https://github.com/HT154[@HT154] for exploring this in https://github.com/apple/pkl/pull/924[#924]!
